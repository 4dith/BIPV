// Each #kernel tells which function to compile; you can have many kernels
#pragma kernel CSMain

struct Ray
{
    float3 origin;
    float3 direction;
};

struct HitRecord
{
    float3 pt;
    float3 normal;
    uint colorIndex;
    float t;
    bool frontFace;
};

struct Sphere
{
    float3 center;
    float radius;
    uint colorIndex;
};

struct Triangle
{
    float3 v0;
    float3 v1;
    float3 v2;
    uint colorIndex;
};

// Create a RenderTexture with enableRandomWrite flag and set it
// with cs.SetTexture
RWTexture2D<float4> Result;
StructuredBuffer<Sphere> Spheres;
StructuredBuffer<Triangle> Triangles;
StructuredBuffer<float3> Colors;

uint NSamples;
uint MaxDepth;
float4x4 CameraToWorld;
float4x4 CameraInverseProj;

uint Hash(uint x)
{
    x ^= x >> 16;
    x *= 0x85ebca6b;
    x ^= x >> 13;
    x *= 0xc2b2ae35;
    x ^= x >> 16;
    return x;
}

float Random(uint seed)
{
    uint hashed = Hash(seed);
    return (float) (hashed & 0x00FFFFFFu) / 16777216.0f; // Normalize to [0, 1]
}

float2 Random2(uint seed)
{
    // Generate the first random number
    float random1 = Random(seed);

    // Modify the seed to generate the second random number
    float random2 = Random(seed + 1);

    return float2(random1, random2);
}

float3 RandomUnitVector(uint seed)
{
    float x = Random(seed) * 2.0f - 1.0f;
    float y = Random(seed + 1) * 2.0f - 1.0f;
    float z = Random(seed + 2) * 2.0f - 1.0f;

    return normalize(float3(x, y, z));
}

Ray CreateRay(float3 origin, float3 direction)
{
    Ray ray;
    ray.origin = origin;
    ray.direction = direction;
    return ray;
}

Ray CreateCameraRay(float2 uv)
{
    float3 origin = mul(CameraToWorld, float4(0.0f, 0.0f, 0.0f, 1.0f)).xyz;
    float3 direction = mul(CameraInverseProj, float4(uv, 0.0f, 1.0f)).xyz;
    direction = mul(CameraToWorld, float4(direction, 0.0f)).xyz;
    // direction = normalize(direction);
    return CreateRay(origin, direction);
}

void SetFaceNormal(inout HitRecord rec, Ray r, float3 outwardNormal)
{
    rec.frontFace = dot(r.direction, outwardNormal) < 0;
    rec.normal = rec.frontFace ? outwardNormal : -outwardNormal;
}

float3 Reflect(float3 v, float3 n)
{
    return v - 2 * dot(v, n) * n;
}

bool HitSphere(Sphere sphere, Ray r, float tMin, float tMax, inout HitRecord rec)
{
    float3 oc = sphere.center - r.origin;
    float a = dot(r.direction, r.direction);
    float h = dot(r.direction, oc);
    float c = dot(oc, oc) - sphere.radius * sphere.radius;
    float discriminant = h * h - a * c;
    
    if (discriminant < 0.0f)
        return false;
    
    float sqrtd = sqrt(discriminant);
    float root = (h - sqrtd) / a;
    if (root <= tMin || tMax <= root)
    {
        root = (h + sqrtd) / a;
        if (root <= tMin || tMax <= root)
            return false;
    }
    
    rec.t = root;
    rec.colorIndex = sphere.colorIndex;
    rec.pt = r.origin + root * r.direction;
    SetFaceNormal(rec, r, (rec.pt - sphere.center) / sphere.radius);
    return true;
}

bool HitTriangle(Triangle tri, Ray r, float tMin, float tMax, inout HitRecord rec)
{
    // Edges
    float3 e1 = tri.v1 - tri.v0;
    float3 e2 = tri.v2 - tri.v0;
    
    // Checking if parallel
    float3 p = cross(r.direction, e2);
    float det = dot(e1, p);
    if (det < 1e-8f)
        return false;
    
    // Check if intersection is outside triangle
    float invdet = 1.0f / det;
    float3 T = r.origin - tri.v0;
    float u = dot(T, p) * invdet;
    if (u < 0.0f || u > 1.0f)
        return false;
    
    float3 q = cross(T, e1);
    float v = dot(r.direction, q) * invdet;
    if (v < 0.0f || u + v > 1.0f)
        return false;
    
    float t = dot(e2, q) * invdet;
    if (t <= tMin || t >= tMax)
        return false;
    
    rec.t = t;
    rec.colorIndex = tri.colorIndex;
    rec.pt = r.origin + t * r.direction;
    SetFaceNormal(rec, r, normalize(cross(e1, e2)));
    return true;
}

float3 RayColor(Ray ray, uint seed)
{    
    HitRecord rec;
    rec.frontFace = false;
    rec.normal = float3(0.0f, 0.0f, 0.0f);
    rec.pt = float3(0.0f, 0.0f, 0.0f);
    rec.t = 0.0f;
    
    bool hitAnything = true;
    uint nSpheres, nTris, _;
    Spheres.GetDimensions(nSpheres, _);
    Triangles.GetDimensions(nTris, _);
    
    float3 attenuation = float3(1.0f, 1.0f, 1.0f);
    
    for (uint depth = 0; depth <= MaxDepth; depth++)
    {
        hitAnything = false;
        float closestSoFar = 1000.0f; // Far clipping plane
        uint i;
        
        for (i = 0; i < nSpheres; i++)
        {
            if (HitSphere(Spheres[i], ray, 0.001f, closestSoFar, rec))
            {
                hitAnything = true;
                closestSoFar = rec.t;
            }
        }
        
        for (i = 0; i < nTris; i++)
        {
            if (HitTriangle(Triangles[i], ray, 0.001f, closestSoFar, rec))
            {
                hitAnything = true;
                closestSoFar = rec.t;
            }
        }
        
        if (hitAnything)
        {
            seed = Hash(seed);
            ray.origin = rec.pt;
            ray.direction = rec.normal + RandomUnitVector(seed);
            
            attenuation *= Colors[rec.colorIndex];
        }
        else
        {
            float3 bgCol = lerp(float3(1.0f, 1.0f, 1.0f), float3(0.5f, 0.7f, 1.0f), 0.5f * (normalize(ray.direction).y + 1.0f));
            return attenuation * bgCol;
        }
    }
    
    return float3(0.0f, 0.0f, 0.0f);
}

[numthreads(8,8,1)]
void CSMain (uint3 id : SV_DispatchThreadID)
{
    // TODO: insert actual code here!
    uint sWidth, sHeight;
    Result.GetDimensions(sWidth, sHeight);
    
    float3 rayColor = float3(0.0f, 0.0f, 0.0f);
    
    for (uint i = 0; i < NSamples; i++)
    {
        uint seed = id.x + id.y * sWidth + i * 2003;
        float2 uv = (id.xy + Random2(seed)) / float2(sWidth, sHeight) * 2.0f - 1.0f;
        Ray ray = CreateCameraRay(uv);
        rayColor += RayColor(ray, seed);
    }
    
    Result[id.xy] = float4(rayColor / NSamples, 1.0f);
}
